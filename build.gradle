plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta8'
}

group = 'me.hotpocket'
version = '2.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "fren_gor"
        url = "https://nexus.frengor.com/repository/public/"
    }
    maven {
        name = "skript-releases"
        url = "https://repo.skriptlang.org/releases"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'com.github.SkriptLang:Skript:2.9.5'
    implementation 'com.frengor:ultimateadvancementapi-shadeable:2.6.0'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.named('shadowJar').configure {
    relocate 'com.frengor.ultimateadvancementapi', 'me.hotpocket.shaded.ultimateadvancementapi'
    archiveClassifier.set('')
}

tasks.named('jar').configure {
    enabled = false
}

artifacts {
    archives tasks.shadowJar
}